buildscript {
    apply from: gradle.dolbyio.files.modules

    dependencies {
        classpath dolbyio.nexus.gradlePlugin
        classpath dolbyio.sonarqube.gradlePlugin
        classpath dolbyio.license.gradlePlugin
        classpath dolbyio.dokka.gradlePlugin
    }
}

plugins {
    alias(dolbyio.plugins.dokka)
    alias(dolbyio.plugins.kotlin.multiplatform) apply false
    alias(dolbyio.plugins.kotlin.jvm) apply false
}

group = "eu.codlab.discord"
version = property("version")

ext.commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()

subprojects {
    group = "eu.codlab.discord"
    version = property("version")

    task allDeps(type: DependencyReportTask) {}

    if (isSourcesModule(it)) {
        apply from: gradle.dolbyio.files.moduleSetup
    }
}

tasks.register("quality") { qualityTask ->
    group = "verification"

    subprojects.each { project ->
        def tasks = ["ktlint", "detekt"].collect {
            project.tasks.named(it)
        }.findAll { it != null }

        qualityTask.dependsOn(tasks)
    }
}